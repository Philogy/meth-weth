/// @dev The only special privileges the lost and found address should have is the ability to
/// receive funds from `address(0)` and `this` addresses (i.e. funds otherwise unaccessible to normal
/// accounts).
#define constant LOST_N_FOUND = 0x1212121212121212121212121212121212121212

#define macro _SWEEP_LOG3(z0) = takes(4) returns(0) {
    // takes:    [indexed_arg2, indexed_arg1, event_hash, amount]
    <z0>      // [indexed_arg2, indexed_arg1, event_hash, amount, 0]
    mstore    // [indexed_arg2, indexed_arg1, event_hash]
    msize     // [indexed_arg2, indexed_arg1, event_hash, 32]
    <z0>      // [indexed_arg2, indexed_arg1, event_hash, 32, 0]
    log3      // []
    // returns:  []
}

/// @dev Should be permissionless-ly callable by anyone, sweeping funds stuck in `address(0)` and
/// `address(this)` to the lost and found. For explicit burning use `0xdead`.
#define macro SWEEP_LOST(z0) = takes(0) returns(0) {
    [LOST_N_FOUND]         // [rescue_dest]
    // -- Load lost tokens.
    <z0>                   // [rescue_dest, 0]
    sload                  // [rescue_dest, zero_bal]
    address                // [rescue_dest, zero_bal, this]
    sload                  // [rescue_dest, zero_bal, this_bal]
    // -- Emit Transfer events.
    dup3                   // [rescue_dest, zero_bal, this_bal, rescue_dest]
    <z0>                   // [rescue_dest, zero_bal, this_bal, rescue_dest, 0]
    __EVENT_HASH(Transfer) // [rescue_dest, zero_bal, this_bal, rescue_dest, 0, Transfer_sig]
    dup5                   // [rescue_dest, zero_bal, this_bal, rescue_dest, 0, Transfer_sig, zero_bal]
    dup4                   // [rescue_dest, zero_bal, this_bal, rescue_dest, 0, Transfer_sig, zero_bal, rescue_dest]
    address                // [rescue_dest, zero_bal, this_bal, rescue_dest, 0, Transfer_sig, zero_bal, rescue_dest, this]
    dup4                   // [rescue_dest, zero_bal, this_bal, rescue_dest, 0, Transfer_sig, zero_bal, rescue_dest, this, Transfer_sig]
    dup8                   // [rescue_dest, zero_bal, this_bal, rescue_dest, 0, Transfer_sig, zero_bal, rescue_dest, this, Transfer_sig, this_bal]
    _SWEEP_LOG3(<z0>)      // [rescue_dest, zero_bal, this_bal, rescue_dest, 0, Transfer_sig, zero_bal]
    _SWEEP_LOG3(<z0>)      // [rescue_dest, zero_bal, this_bal]
    // -- Increase the balance of the sweep address.
    add                    // [rescue_dest, rescue_total]
    dup2                   // [rescue_dest, rescue_total, rescue_dest]
    sload                  // [rescue_dest, rescue_total, rescue_dest_bal]
    add                    // [rescue_dest, updated_rescue_bal]
    swap1                  // [updated_rescue_bal, rescue_dest]
    sstore                 // []
    // -- Reset balance of the "lost accounts".
    <z0> <z0>              // [0, 0]
    sstore                 // []
    <z0>                   // [0]
    address                // [0, this]
    sstore                 // []
    stop                   // [] -- end
}
