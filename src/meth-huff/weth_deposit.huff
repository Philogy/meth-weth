#include "../interfaces/IMETH.huff"

////////////////////////////////////////////////////////////////
//               DEPOSIT (AKA WRAPPING) METHODS               //
////////////////////////////////////////////////////////////////


#define macro _EMIT_DEPOSIT(z0) = takes(1) returns(0) {
    // takes:                 [to]
    callvalue              // [to, callvalue]
    <z0>                   // [to, callvalue, 0]
    mstore                 // [to]
    __EVENT_HASH(Deposit)  // [to, Deposit.sig]
    0x10                   // [to, Deposit.sig, 16]
    dup1                   // [to, Deposit.sig, 16, 16]
    log2                   // []
    // returns:               []
}

#define macro _DEPOSIT(z0) = takes(0) returns(0) {
    // takes:     []
    caller     // [caller]
    sload      // [caller.bal]
    callvalue  // [caller.bal, callvalue]
    add        // [caller_bal']
    caller     // [caller_bal', caller]
    sstore     // []
    caller     // [caller]
    _EMIT_DEPOSIT(<z0>)
    //            []
    // returns:   []
}

#define macro DEPOSIT(z0) = takes(0) returns(0) {
    _DEPOSIT(<z0>)        // []
    stop                  // [] -- end
}

#define macro DEPOSIT_TO(z0) = takes(0) returns(0) {
    // takes:                     []
    _LOAD_ADDRESS(0x10)        // [to]
    dup1                       // [to, to]
    sload                      // [to, to.bal]
    callvalue                  // [to, to.bal, callvalue]
    add                        // [to, to.bal']
    dup2                       // [to, to.bal', to]
    sstore                     // [to]
    _EMIT_DEPOSIT(<z0>)        // []
    stop                       // [] -- end
}

#define macro DEPOSIT_AND_APPROVE(z0) = takes(0) returns(0) {
    _DEPOSIT(<z0>)        // []
    _APPROVE(<z0>)        // []
    stop                  // [] -- end
}
