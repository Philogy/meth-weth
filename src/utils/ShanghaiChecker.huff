#define function shanghaiEnabled() view returns (bool)
#define function usePush0() view returns ()

#define macro _MAIN(zero) = takes(0) returns(0) {
    <zero> calldataload 0xe0 shr // [selector]
    dup1 __FUNC_SIG(shanghaiEnabled) eq shanghaiEnabled jumpi
         __FUNC_SIG(usePush0)        eq usePush0        jumpi

    empty_revert:
        <zero> <zero> revert

    shanghaiEnabled:
        __FUNC_SIG(usePush0)     // [usePush0_selector]
        msize                    // [0x0, usePush0_selector]
        mstore                   // []

        <zero> <zero>            // [0, 0]
        0x04                     // [4, 0, 0]
        0x1c                     // [28, 4, 0, 0]
        address                  // [this, 28, 4, 0, 0]
        0xff                     // [255, this, 28, 4, 0, 0]
        staticcall               // [success]
        <zero>                   // [0, success]
        mstore                   // []
        msize                    // [0x20]
        <zero>                   // [0x00, 0x20]
        return
    usePush0:
        address caller sub       // [this != caller]
        empty_revert jumpi
        push0
        stop
}

#define macro MAIN() = takes(0) returns(0) {
    callvalue empty_revert jumpi

    _MAIN(callvalue)
}
