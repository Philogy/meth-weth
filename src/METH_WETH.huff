#include "./interfaces/IMETH.huff"

////////////////////////////////////////////////////////////////
//             SELECTOR SWITCH CONSTANTS                      //
////////////////////////////////////////////////////////////////
/// @dev Right bit shift that retrieves the unique upper bits of all selectors contained within the ABI.
#define constant S_SHIFT = 0x12 // 18
#define constant S_COVER = 0x3f // 63 (111111_2)

////////////////////////////////////////////////////////////////
//                 METADATA CONSTANTS                         //
////////////////////////////////////////////////////////////////
// ================== Decimals ===================
/// @dev METH has 18 decimals meaning a balance of `1150100000000000000` should be displayed as `1.1501`.
#define constant METH_DECIMALS = 0x12
// ==================== Name =====================
#define table NAME_DATA {
    0x000000000000000000000000000000000000000000000000000000000000002000000000000000000000000000000000000000000000000000000000000000214d6178696d616c6c7920456666696369656e742057726170706564204574686572
}
// =================== Symbol ====================
// symbol.length ++ "METH"
#define constant PACKED_SYMBOL = 0x044d455448000000000000000000000000000000000000000000000000000000

////////////////////////////////////////////////////////////////
//                      OTHER CONSTANTS                       //
////////////////////////////////////////////////////////////////
/// @dev The only special privileges the recovery address should have is the ability to sweep funds from the zero and this addresses.
#define constant LOST_N_FOUND = 0x1212121212121212121212121212121212121212

////////////////////////////////////////////////////////////////
//                       UTILITY MACROS                       //
////////////////////////////////////////////////////////////////

#define macro _NOTA_AND_B() = takes(2) returns(1) {
    // takes:          [b, a]

    // Need (A == 0)  &&   B:
    //         [=0]   LT  [0] -> 0
    //         [=0]   LT  [1] -> 1
    //         [>0]   LT  [0] -> 0
    //         [>0]   LT  [1] -> 0

    lt              // [!a && b]
    // returns:        [res]
}

#define macro _LOAD_ADDRESS() = takes(1) returns(1) {
    // takes:             [addr_offset]
    calldataload       // [shifted_addr]
    0x60               // [shifted_addr, 96]
    shr                // [addr]
    // returns:        // [addr]
}

#define macro _IS_INF() = takes(1) returns(1) {
    // takes:      [allowance]
    push0       // [allowance, 0]
    byte        // [is_infinite]
    // returns:    [is_infinite]
}

#define macro _RETURN_WORD(word_size) = takes(1) returns(0) {
    // takes:      [x]
    push0       // [x, 0] 
    mstore      // []
    <word_size> // [32]
    push0       // [32, 0]
    return      // []
}

#define macro _SEND_ETH() = takes(6) returns(0) {
    // takes:                [0, 0, 0, 0, amount, recipient]
    gas                   // [0, 0, 0, 0, amount, recipient, ags]
    call                  // [success]
    no_revert jumpi       // []
        returndatasize    // [rdz]
        push0 push0       // [rdz, 0, 0]
        returndatacopy    // []
        returndatasize    // [rdz]
        push0             // [rdz, 0]
        revert            // [] -- end
    no_revert:            // []
    // Stop in `_SEND_ETH` macro to ensure CEI.
        stop              // [] -- end
}

////////////////////////////////////////////////////////////////
//                       EVENT HELPERS                        //
////////////////////////////////////////////////////////////////

#define macro _EMIT_TRANSFER(word_size) = takes(3) returns(0) {
    // takes:                 [to, from, amount]
    push0                  // [to, from, amount, 0]
    mstore                 // [to, from]
    __EVENT_HASH(Transfer) // [to, from, Transfer.sig]
    <word_size>            // [to, from, Transfer.sig, 32]
    push0                  // [to, from, Transfer.sig, 32, 0]
    log3                   // []
    // returns:               []
}

#define macro _EMIT_DEPOSIT(word_size) = takes(1) returns(0) {
    // takes:                 [to]
    callvalue              // [to, callvalue]
    push0                  // [to, callvalue, 0]
    mstore                 // [to]
    __EVENT_HASH(Deposit)  // [to, Deposit.sig]
    <word_size>            // [to, Deposit.sig, 32]
    push0                  // [to, Deposit.sig, 32, 0]
    log2                   // []
    // returns:               []
}

#define macro _EMIT_WITHDRAWAL(word_size) = takes(2) returns(0) {
    // takes:                   [from, amount]
    push0                    // [from, amount, 0]
    mstore                   // [from]
    __EVENT_HASH(Withdrawal) // [from, Withdrawal.sig]
    <word_size>              // [from, Withdrawal.sig, 32]
    push0                    // [from, Withdrawal.sig, 32, 0]
    log2                     // []
    // returns:                 []
}

#define macro _SWEEP_LOG3() = takes(4) returns(0) {
    // takes:    [indexed_arg2, indexed_arg1, event_hash, amount]
    push0     // [indexed_arg2, indexed_arg1, event_hash, amount, 0]
    mstore    // [indexed_arg2, indexed_arg1, event_hash]
    msize     // [indexed_arg2, indexed_arg1, event_hash, 32]
    push0     // [indexed_arg2, indexed_arg1, event_hash, 32, 0]
    log3      // []
    // returns:  []
}


////////////////////////////////////////////////////////////////
//                  REUSABLE FUNCTION MACROS                  //
////////////////////////////////////////////////////////////////


#define macro _TRANSFER(word_size) = takes(6) returns(0) {
    // takes:                      [has_error, from, amount, to, amount, from]
    // -- Update `from` Balance.
    sload                       // [has_error, from, amount, to, amount, from.bal]
    dup3                        // [has_error, from, amount, to, amount, from.bal, to]
    swap6                       // [to, from, amount, to, amount, from.bal, has_error]
    dup2                        // [to, from, amount, to, amount, from.bal, has_error, from.bal]
    dup4                        // [to, from, amount, to, amount, from.bal, has_error, from.bal, amount]
    gt                          // [to, from, amount, to, amount, from.bal, has_error, amount > from.bal]
    or                          // [to, from, amount, to, amount, from.bal, has_error']
    empty_revert jumpi          // [to, from, amount, to, amount, from.bal]
    sub                         // [to, from, amount, to, from.bal']
    dup4                        // [to, from, amount, to, from.bal', from]
    sstore                      // [to, from, amount, to]
    // -- Update `to` Balance.
    sload                       // [to, from, amount, to.bal]
    dup2                        // [to, from, amount, to.bal, amount]
    add                         // [to, from, amount, to.bal']
    dup4                        // [to, from, amount, to.bal', to]
    sstore                      // [to, from, amount]
    _EMIT_TRANSFER(<word_size>) // []
    // returns:                    []
}

#define macro _WITHDRAW_ALL() = takes(0) returns(1) {
    // takes:                  []
    // -- Update balance.
    caller                  // [caller]
    sload                   // [caller.bal]
    push0                   // [caller.bal, 0]
    caller                  // [caller.bal, 0, caller]
    sstore                  // [caller.bal]
    // -- Emit event.
    caller                  // [caller.bal, caller]
    dup2                    // [caller.bal, caller, caller.bal]
    _EMIT_WITHDRAWAL(msize) // [caller.bal]
    // returns:                [caller.bal]
}

/// @dev Expects msize to be 0-1 words.
#define macro _APPROVE() = takes(0) returns(0) {
    0x24 calldataload      // [amount]
    0x10 _LOAD_ADDRESS()   // [amount, spender]

    // -- Emit Event.
    dup2                   // [amount, spender, amount]
    push0                  // [amount, spender, amount, 0]
    mstore                 // [amount, spender]
    dup1                   // [amount, spender, spender]
    caller                 // [amount, spender, spender, caller]
    __EVENT_HASH(Approval) // [amount, spender, spender, caller, Approval.sig]
    msize                  // [amount, spender, spender, caller, Approval.sig, 32]
    push0                  // [amount, spender, spender, caller, Approval.sig, 32, 0]
    log3                   // [amount, spender]

    // -- Get Allowance Slot.
    caller                 // [amount, spender, caller]
    push0                  // [amount, spender, caller, 0]
    mstore                 // [amount, spender]
    msize                  // [amount, spender, 32]
    mstore                 // [amount]
    msize                  // [amount, 64]
    push0                  // [amount, 64, 0]
    sha3                   // [amount, allowance_slot]
    // -- Update Allowance.
    sstore                 // []
}

#define macro _DEPOSIT() = takes(0) returns(0) {
    // takes:               []
    caller               // [caller]
    sload                // [caller_bal]
    callvalue            // [callvalue]
    add                  // [caller_bal']
    caller               // [caller, caller_bal']
    sstore               // []
    caller               // [caller]
    _EMIT_DEPOSIT(msize) // []
    // returns:             []
}

#define macro _BURN_FROM_CALLER() = takes(5) returns(5) {
    // takes:                  [has_error, 0, 0, 0, amount]
    dup1                    // [has_error, 0, 0, 0, amount, amount]
    caller                  // [has_error, 0, 0, 0, amount, amount, caller]
    sload                   // [has_error, 0, 0, 0, amount, amount, caller.bal]
    push0                   // [has_error, 0, 0, 0, amount, amount, caller.bal, 0]
    swap7                   // [0, 0, 0, 0, amount, amount, caller.bal, has_error]
    dup2                    // [0, 0, 0, 0, amount, amount, caller.bal, has_error, caller.bal]
    dup4                    // [0, 0, 0, 0, amount, amount, caller.bal, has_error, caller.bal, amount]
    gt                      // [0, 0, 0, 0, amount, amount, caller.bal, has_error, amount > caller.bal]
    or                      // [0, 0, 0, 0, amount, amount, caller.bal, has_error']
    empty_revert jumpi      // [0, 0, 0, 0, amount, amount, caller.bal]
    sub                     // [0, 0, 0, 0, amount, caller.bal']
    caller                  // [0, 0, 0, 0, amount, caller.bal', caller]
    sstore                  // [0, 0, 0, 0, amount]
    caller                  // [0, 0, 0, 0, amount, caller]
    dup2                    // [0, 0, 0, 0, amount, caller, amount]
    _EMIT_WITHDRAWAL(msize) // [0, 0, 0, 0, amount]
    // returns:                [0, 0, 0, 0, amount]
}

/**
 * @dev Determines the allowance slot for `(from, caller)` and loads the allowance. Jumps to
 * `inf_allow_dest` if the allowance is infinite. Expects msize to be 0-1 words.
 */
#define macro _GET_ALLOWANCE(inf_allow_dest) = takes(1) returns(2) {
    // takes:                 [from]

    // -- Calculate allowance slot.
    push0                  // [from, 0]
    mstore                 // []
    caller                 // [caller]
    msize                  // [caller, 32]
    mstore                 // []
    msize                  // [64]
    push0                  // [64, 0]
    sha3                   // [allowance_slot]
    // -- Load allowance.
    dup1                   // [allowance_slot, allowance_slot]
    sload                  // [allowance_slot, allowance]
    // -- Check infinite.
    dup1                   // [allowance_slot, allowance, allowance]
    _IS_INF()              // [allowance_slot, allowance, allowance_infinite]
    <inf_allow_dest> jumpi // [allowance_slot, allowance]
    // returns:               [allowance_slot, allowance]
}

////////////////////////////////////////////////////////////////
//                      METADATA METHODS                      //
////////////////////////////////////////////////////////////////

#define macro NAME() = takes(0) returns(0) {
    __tablesize(NAME_DATA)  // [name_data.size]
    __tablestart(NAME_DATA) // [name_data.size, name_data.offset]
    push0                   // [name_data.size, name_data.offset, 0]
    codecopy                // []
    msize                   // [ret_size]
    push0                   // [ret_size, 0]
    return                  // [] -- end
}

#define macro SYMBOL() = takes(0) returns(0) {
    0x20             // [32]
    push0            // [32, 0]
    mstore           // []
    [PACKED_SYMBOL]  // [packed_symbol]
    0x3f             // [packed_symbol, 63]
    mstore           // []
    msize            // [ret_size]
    push0            // [ret_size, 0]
    return           // [] -- end
}

#define macro DECIMALS() = takes(0) returns(0) {
    [METH_DECIMALS]       // [decimals]
    _RETURN_WORD(msize)   // [] -- end
}

////////////////////////////////////////////////////////////////
//                        VIEW METHODS                        //
////////////////////////////////////////////////////////////////

#define macro TOTAL_SUPPLY() = takes(0) returns(0) {
    selfbalance           // [this.balance]
    _RETURN_WORD(msize)   // [] -- end
}

#define macro BALANCE_OF() = takes(0) returns(0) {
    0x10 _LOAD_ADDRESS()  // [account]
    sload                 // [account.bal]
    _RETURN_WORD(msize)   // [] -- end
}

#define macro ALLOWANCE() = takes(0) returns(0) {
    0x34               // [52]
    // Copying from start of address intead of zero to mitigate risk of dirty address resulting in
    // valid address.
    0x10               // [52, 16]
    0x0c               // [52, 16, 12]
    calldatacopy       // []
    msize              // [64]
    push0              // [64, 0]
    sha3               // [allowance_slot]
    sload              // [allowance]
    push0              // [allowance, 0]
    mstore             // []
    0x20               // [32]
    push0              // [32, 0]
    return             // [] -- end
}

#define macro TRANSFER() = takes(1) returns(0) {
    // takes:                [has_error]
    caller                // [has_error, caller]
    0x24 calldataload     // [has_error, caller, amount]
    0x10 _LOAD_ADDRESS()  // [has_error, caller, amount, to]
    dup2                  // [has_error, caller, amount, to, amount]
    caller                // [has_error, caller, amount, to, amount, caller]
    _TRANSFER(msize)      // []
    stop                  // [] -- end
}

#define macro _TRANSFER_FROM_END() = takes(4) returns(0) {
    // takes:                 [has_error, from, amount, to]
    dup2                   // [has_error, from, amount, to, amount]
    dup4                   // [has_error, from, amount, to, amount, from]
    _TRANSFER(0x20)        // []
    stop                   // [] -- end
}


/// @dev Macro should not be reachable.
#define macro _TRANSFER_FROM_INF_END() = takes(0) returns(0) {
    transferFrom_inf_allow:
        //                      [has_error, from, amount, to, _, _]
        pop pop              // [has_error, from, amount, to]
        _TRANSFER_FROM_END() // [] -- end
}

#define macro TRANSFER_FROM() = takes(1) returns(0) {
    // takes:                [has_error]
    0x10 _LOAD_ADDRESS()  // [has_error, from]
    0x44 calldataload     // [has_error, from, amount]
    0x30 _LOAD_ADDRESS()  // [has_error, from, amount, to]
    // -- Get allowance slot & load.
    dup3                  // [has_error, from, amount, to, from]
    _GET_ALLOWANCE(transferFrom_inf_allow)
    //                       [has_error, from, amount, to, allowance_slot, allowance]
    // -- Check sufficient allowance.
    swap5                 // [allowance, from, amount, to, allowance_slot, has_error]
    dup6                  // [allowance, from, amount, to, allowance_slot, has_error, allowance]
    dup5                  // [allowance, from, amount, to, allowance_slot, has_error, allowance, amount]
    gt                    // [allowance, from, amount, to, allowance_slot, has_error, amount > allowance]
    or                    // [allowance, from, amount, to, allowance_slot, has_error']
    swap5                 // [has_error, from, amount, to, allowance_slot, allowance]
    // -- Update allowance.
    dup4                  // [has_error, from, amount, to, allowance_slot, allowance, amount]
    swap1                 // [has_error, from, amount, to, allowance_slot, amount, allowance]
    sub                   // [has_error, from, amount, to, allowance_slot, allowance']
    swap1                 // [has_error, from, amount, to, allowance', allowance_slot]
    sstore                // [has_error, from, amount, to]
    _TRANSFER_FROM_END()  // []
}

#define macro DEPOSIT() = takes(0) returns(0) {
    _DEPOSIT()            // []
    stop                  // [] -- end
}

#define macro DEPOSIT_TO() = takes(0) returns(0) {
    // -- Load params.
    0x10 _LOAD_ADDRESS()  // [to]
    dup1                  // [to, to]
    sload                 // [to, to.bal]
    callvalue             // [to, to.bal, callvalue]
    add                   // [to, to.bal']
    dup2                  // [to, to.bal', to]
    sstore                // [to]
    _EMIT_DEPOSIT(msize)  // []
    stop                  // [] -- end
}

#define macro DEPOSIT_AND_APPROVE() = takes(0) returns(0) {
    _DEPOSIT()            // []
    _APPROVE()            // []
    stop                  // [] -- end
}

#define macro APPROVE() = takes(0) returns(0) {
    _APPROVE()      // []
    stop            // [] -- end
}

#define macro WITHDRAW() = takes(1) returns(0) {
    // takes:              [has_error]
    push0 push0 push0   // [has_error, 0, 0, 0]
    0x04 calldataload   // [has_error, 0, 0, 0, amount]
    _BURN_FROM_CALLER() // [0, 0, 0, 0, amount]
    caller              // [0, 0, 0, 0, amount, caller]
    _SEND_ETH()         // [] -- end
}

#define macro WITHDRAW_TO() = takes(1) returns(0) {
    // takes:                  [has_error]
    push0 push0 push0       // [has_error, 0, 0, 0]
    0x24 calldataload       // [has_error, 0, 0, 0, amount]
    _BURN_FROM_CALLER()     // [0, 0, 0, 0, amount]
    0x04 calldataload       // [0, 0, 0, 0, amount, to]
    _SEND_ETH()             // [] -- end
}

#define macro WITHDRAW_ALL() = takes(0) returns(0) {
    // takes:                  []
    push0 push0 push0 push0 // [0, 0, 0, 0]
    _WITHDRAW_ALL()         // [0, 0, 0, 0, amount]
    caller                  // [0, 0, 0, 0, amount, caller]
    _SEND_ETH()             // []
}

#define macro WITHDRAW_ALL_TO() = takes(0) returns(0) {
    // takes:                  []
    // Push zeros before to avoid swaps later.
    push0 push0 push0 push0 // [0, 0, 0, 0]
    _WITHDRAW_ALL()         // [0, 0, 0, 0, amount]
    0x04 calldataload       // [0, 0, 0, 0, amount, to]
    _SEND_ETH()             // []
}

#define macro _WITHDRAW_FROM_BAL_CHECK() = takes(6) returns(0) {
    // takes:                      [no_error, 0, 0, 0, amount, from]
    dup1                        // [no_error, 0, 0, 0, amount, from, from]
    sload                       // [no_error, 0, 0, 0, amount, from, from_bal]
    swap6                       // [from_bal, 0, 0, 0, amount, from, no_error]
    dup7                        // [from_bal, 0, 0, 0, amount, from, no_error, from_bal]
    dup4                        // [from_bal, 0, 0, 0, amount, from, no_error, from_bal, amount]
    gt                          // [from_bal, 0, 0, 0, amount, from, no_error, amount > from_bal]
    _NOTA_AND_B()               // [from_bal, 0, 0, 0, amount, from, no_error']
    complete_withdrawFrom jumpi // [from_bal, 0, 0, 0, amount, from]
    push0 push0                 // [from_bal, 0, 0, 0, amount, from, 0, 0]
    revert                      // [from_bal, 0, 0, 0, amount, from]
}

#define macro _WITHDRAW_FROM_START() = takes(1) returns(0) {
    // takes:                     [no_error]

    // -- Initial stack setup.
    push0 push0 push0          // [no_error, 0, 0, 0]
    0x24 calldataload          // [no_error, 0, 0, 0, amount]
    0x10 _LOAD_ADDRESS()       // [no_error, 0, 0, 0, amount, from]
    dup1                       // [no_error, 0, 0, 0, amount, from, from]
    // -- Allowance load & check.
    _GET_ALLOWANCE(withdrawFrom_inf_allow)
    //                            [no_error,  0, 0, 0, amount, from, allowance_slot, allowance]
    swap7                      // [allowance, 0, 0, 0, amount, from, allowance_slot, no_error]
    dup8                       // [allowance, 0, 0, 0, amount, from, allowance_slot, no_error, allowance]
    dup5                       // [allowance, 0, 0, 0, amount, from, allowance_slot, no_error, allowance, amount]
    gt                         // [allowance, 0, 0, 0, amount, from, allowance_slot, no_error, amount > allowance]
    _NOTA_AND_B()              // [allowance, 0, 0, 0, amount, from, allowance_slot, no_error']
    swap7                      // [no_error', 0, 0, 0, amount, from, allowance_slot, allowance]
    // -- Update allowance.
    dup4                       // [no_error', 0, 0, 0, amount, from, allowance_slot, allowance, amount]
    swap1                      // [no_error', 0, 0, 0, amount, from, allowance_slot, amount, allowance]
    sub                        // [no_error', 0, 0, 0, amount, from, allowance_slot, allowance']
    swap1                      // [no_error', 0, 0, 0, amount, from, allowance', allowance_slot]
    sstore                     // [no_error', 0, 0, 0, amount, from]
    _WITHDRAW_FROM_BAL_CHECK() // [] -- end
}

#define macro _WITHDRAW_FROM_END() = takes(0) returns(0) {
    withdrawFrom_inf_allow:        // [no_error, 0, 0, 0, amount, from, allowance_slot, allowance]
        pop pop                    // [no_error, 0, 0, 0, amount, from]
        _WITHDRAW_FROM_BAL_CHECK() // [] -- end
    complete_withdrawFrom:         // [from.bal, 0, 0, 0, amount, from]
        // -- Update balance.
        dup2                       // [from.bal, 0, 0, 0, amount, from, amount]
        push0                      // [from.bal, 0, 0, 0, amount, from, amount, 0]
        swap7                      // [0,        0, 0, 0, amount, from, amount, from.bal]
        sub                        // [0,        0, 0, 0, amount, from, from.bal']
        dup2                       // [0,        0, 0, 0, amount, from, from.bal', from]
        sstore                     // [0,        0, 0, 0, amount, from]
        dup2                       // [0,        0, 0, 0, amount, from, amount]
        _EMIT_WITHDRAWAL(0x20)     // [0,        0, 0, 0, amount]
        caller                     // [0,        0, 0, 0, amount, caller]
        _SEND_ETH()                // [] -- end
}

#define macro _WITHDRAW_FROM_TO_END() = takes(6) returns(0) {
    // takes:                 [has_error, 0, 0, 0, amount, from]
    dup1                   // [has_error, 0, 0, 0, amount, from, from]
    sload                  // [has_error, 0, 0, 0, amount, from, from.bal]
    swap6                  // [from.bal,  0, 0, 0, amount, from, has_error]
    dup7                   // [from.bal,  0, 0, 0, amount, from, has_error, from.bal]
    dup4                   // [from.bal,  0, 0, 0, amount, from, has_error, from.bal, amount]
    gt                     // [from.bal,  0, 0, 0, amount, from, has_error, amount > from.bal]
    or                     // [from.bal,  0, 0, 0, amount, from, has_error']
    empty_revert jumpi     // [from.bal,  0, 0, 0, amount, from]
    dup2                   // [from.bal,  0, 0, 0, amount, from, amount]
    push0                  // [from.bal,  0, 0, 0, amount, from, amount, 0]
    swap7                  // [0, 0, 0, 0, amount, from, amount, from.bal]
    sub                    // [0, 0, 0, 0, amount, from, from.bal']
    dup2                   // [0, 0, 0, 0, amount, from, from.bal', from]
    sstore                 // [0, 0, 0, 0, amount, from]
    dup2                   // []
    _EMIT_WITHDRAWAL(0x20) // [0, 0, 0, 0, amount]
    0x24 calldataload      // [0, 0, 0, 0, amount, to]
    _SEND_ETH()            // [] -- end
}

#define macro _WITHDRAW_FROM_TO_INF_END() = takes(7) returns(0) {
    withdrawFromTo_inf_allow:   // [has_error, 0, 0, 0, amount, from, allowance_slot, allowance]
        pop pop                 // [has_error, 0, 0, 0, amount, from]
        _WITHDRAW_FROM_TO_END() // [] -- end
}

#define macro WITHDRAW_FROM_TO() = takes(1) returns(0) {
    // takes:                  [has_error]
    // -- Initial stack setup.
    push0 push0 push0       // [has_error, 0, 0, 0]
    0x44 calldataload       // [has_error, 0, 0, 0, amount]
    0x10 _LOAD_ADDRESS()    // [has_error, 0, 0, 0, amount, from]
    dup1                    // [has_error, 0, 0, 0, amount, from, from]
    // -- Allowance load & check.
    _GET_ALLOWANCE(withdrawFromTo_inf_allow)
    //                         [has_error, 0, 0, 0, amount, from, allowance_slot, allowance]
    swap7                   // [allowance, 0, 0, 0, amount, from, allowance_slot, has_error]
    dup8                    // [allowance, 0, 0, 0, amount, from, allowance_slot, has_error, allowance]
    dup5                    // [allowance, 0, 0, 0, amount, from, allowance_slot, has_error, allowance, amount]
    gt                      // [allowance, 0, 0, 0, amount, from, allowance_slot, has_error, amount > allowance]
    or                      // [allowance, 0, 0, 0, amount, from, allowance_slot, has_error']
    swap7                   // [has_error, 0, 0, 0, amount, from, allowance_slot, allowance]
    // -- Update allowance.
    dup4                    // [has_error, 0, 0, 0, amount, from, allowance_slot, allowance, amount]
    swap1                   // [has_error, 0, 0, 0, amount, from, allowance_slot, amount, allowance]
    sub                     // [has_error, 0, 0, 0, amount, from, allowance_slot, allowance']
    swap1                   // [has_error, 0, 0, 0, amount, from, allowance', allowance_slot]
    sstore                  // [has_error, 0, 0, 0, amount, from]
    _WITHDRAW_FROM_TO_END() // -- end
}

#define macro SWEEP_LOST() = takes(0) returns(0) {
    [LOST_N_FOUND]         // [sweep_dest]
    // -- Load lost tokens.
    push0                  // [sweep_dest, 0]
    sload                  // [sweep_dest, 0.bal]
    address                // [sweep_dest, 0.bal, this]
    sload                  // [sweep_dest, 0.bal, this.bal]
    // -- Emit Transfer events.
    dup3                   // [sweep_dest, 0.bal, this.bal, sweep_dest]
    push0                  // [sweep_dest, 0.bal, this.bal, sweep_dest, 0]
    __EVENT_HASH(Transfer) // [sweep_dest, 0.bal, this.bal, sweep_dest, 0, Transfer.sig]
    dup5                   // [sweep_dest, 0.bal, this.bal, sweep_dest, 0, Transfer.sig, 0.bal]
    dup4                   // [sweep_dest, 0.bal, this.bal, sweep_dest, 0, Transfer.sig, 0.bal, sweep_dest]
    address                // [sweep_dest, 0.bal, this.bal, sweep_dest, 0, Transfer.sig, 0.bal, sweep_dest, this]
    dup4                   // [sweep_dest, 0.bal, this.bal, sweep_dest, 0, Transfer.sig, 0.bal, sweep_dest, this, Transfer.sig]
    dup8                   // [sweep_dest, 0.bal, this.bal, sweep_dest, 0, Transfer.sig, 0.bal, sweep_dest, this, Transfer.sig, this.bal]
    _SWEEP_LOG3()          // [sweep_dest, 0.bal, this.bal, sweep_dest, 0, Transfer.sig, 0.bal]
    _SWEEP_LOG3()          // [sweep_dest, 0.bal, this.bal]
    // -- Increase the balance of the sweep address.
    add                    // [sweep_dest, sweep_total
    dup2                   // [sweep_dest, sweep_total, sweep_dest]
    sload                  // [sweep_dest, sweep_total, sweep_dest.bal]
    add                    // [sweep_dest, sweep_dest.bal']
    swap1                  // [sweep_dest.bal', sweep_dest]
    sstore                 // []
    // -- Reset balance of the "lost accounts".
    push0 push0            // [0, 0]
    sstore                 // []
    push0                  // [0]
    address                // [0, this]
    sstore                 // []
    stop
}

#define macro __SELECTOR() = takes(0) returns(1) {
    // takes:       []
    push0        // [0]
    calldataload // [cd[0:32]]
    0xe0         // [224, cd[0:32]]
    shr          // [selector]
    // returns:     [selector]
}


/// @dev Size: 8
#define macro INVALID_NON_PAYABLE() = takes(2) returns(1) {
    // takes:                [msg.sig, selector]
    sub                   // [selector != msg.sig]
    callvalue             // [selector != msg.sig, callvalue]
    or                    // [selector != msg.sig || (msg.value != 0)]
    // returns:              [invalid]
}

/// @dev Size: 3
#define macro VALID_NON_PAYABLE() = takes(2) returns(1) {
    // takes:                [selector, msg.sig]
    eq                    // [selector == msg.sig]
    callvalue             // [msg.value, selector == msg.sig]
    _NOTA_AND_B()         // [no_error]
}

/// @dev Size: 12
#define macro __NON_PAYABLE_SELECTOR_CHECK() = takes(2) returns(0) {
    // takes:                [msg.sig, selector]
    INVALID_NON_PAYABLE() // [invalid]
    empty_revert jumpi    // []
}

/// @dev Size: 10
#define macro __PAYABLE_SELECTOR_CHECK() = takes(2) returns(0) {
    // takes:             [msg.sig, selector]
    sub                // [selector != msg.sig]
    empty_revert jumpi // []
}

#define macro __NO_MATCH() = takes(0) returns(0) {
    push0 push0 revert
    // Padding to make sure block is 64 bytes large post compilation.
    /* padding (60) */ stop stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                       stop stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                       stop stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                       stop stop stop stop stop stop stop stop stop stop stop stop stop stop stop
}

#define macro __RECEIVE_CHECK() = takes(0) returns(0) {
    calldatasize empty_revert jumpi
}

#define macro __FN_DISPATCHER_CORE() = takes(0) returns(1) {
    __SELECTOR()        // [selector]
    // -- Isolate unique selector bits.
    dup1                // [selector, selector]
    [S_SHIFT]           // [selector, selector, sel_shift]
    shr                 // [selector, unique_sel_bits]
    // -- Fixed mask for lower bits.
    [S_COVER]           // [selector, unique_sel_bits, sel_cover]
    or                  // [selector, jump_dest]
    jump             // @> [selector]
}

#define macro MAIN() = takes(0) returns(0) {
    __FN_DISPATCHER_CORE()

        // Padding to make sure jump dests start at the right offset.
        /* padding (50) */ stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop

    dest_0x00:
        __RECEIVE_CHECK()
        DEPOSIT()
        /* padding (11) */ stop stop stop stop stop stop stop stop stop stop stop
    dest_0x01: __NO_MATCH()
    dest_0x02: __NO_MATCH()
    dest_0x03: __NO_MATCH()
    dest_0x04: __NO_MATCH()
    dest_0x05: __NO_MATCH()
    dest_0x06:
        // 0x06fdde03
        __FUNC_SIG(name)
        __NON_PAYABLE_SELECTOR_CHECK()
        NAME()
        /* padding (41) */ stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop
    dest_0x07: __NO_MATCH()
    dest_0x08: __NO_MATCH()
    dest_0x09:
        // 0x095ea7b3
        __FUNC_SIG(approve) 
        __NON_PAYABLE_SELECTOR_CHECK()
        APPROVE()
        /// @dev Selectors 0x0a000000 - 0x0affffff will exceptionally revert.
        /* padding (55) */ stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop
    dest_0x0b: __NO_MATCH()
    dest_0x0c: __NO_MATCH()
    dest_0x0d: __NO_MATCH()
    dest_0x0e: __NO_MATCH()
    dest_0x0f: __NO_MATCH()
    dest_0x10: __NO_MATCH()
    dest_0x11: __NO_MATCH()
    dest_0x12: __NO_MATCH()
    dest_0x13: __NO_MATCH()
    dest_0x14: __NO_MATCH()
    dest_0x15: __NO_MATCH()
    dest_0x16: __NO_MATCH()
    dest_0x17: __NO_MATCH()
    dest_0x18:
        // 0x18160ddd
        __FUNC_SIG(totalSupply)
        __NON_PAYABLE_SELECTOR_CHECK()
        TOTAL_SUPPLY()
        /* padding (45) */ stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop
    dest_0x19: __NO_MATCH()
    dest_0x1a: __NO_MATCH()
    dest_0x1b: __NO_MATCH()
    dest_0x1c: __NO_MATCH()
    dest_0x1d: __NO_MATCH()
    dest_0x1e: __NO_MATCH()
    dest_0x1f: __NO_MATCH()
    dest_0x20:
        // 0x205c2878
        __FUNC_SIG(withdrawTo)
        INVALID_NON_PAYABLE()
        WITHDRAW_TO()
        /// @dev Selectors 0x21000000 - 0x21ffffff will exceptionally revert.
        /* padding (39) */ stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop
    dest_0x22: __NO_MATCH()
    dest_0x23:
        // 0x23b872dd
        __FUNC_SIG(transferFrom)
        INVALID_NON_PAYABLE()
        TRANSFER_FROM()
        /// @dev Selectors 0x24000000 - 0x24ffffff will exceptionally revert.
        /* padding (14) */ stop stop stop stop stop stop stop stop stop stop stop stop stop stop
    dest_0x25: __NO_MATCH()
    dest_0x26: __NO_MATCH()
    dest_0x27: __NO_MATCH()
    dest_0x28:
        // 0x28026ace
        __FUNC_SIG(depositAndApprove)
        __PAYABLE_SELECTOR_CHECK()
        DEPOSIT_AND_APPROVE()
        /// @dev Selectors 0x29000000 - 0x29ffffff will exceptionally revert.
        /* padding (11) */ stop stop stop stop stop stop stop stop stop stop stop
    dest_0x2a: __NO_MATCH()
    dest_0x2b: __NO_MATCH()
    dest_0x2c: __NO_MATCH()
    dest_0x2d: __NO_MATCH()
    dest_0x2e:
        // 0x2e1a7d4d
        __FUNC_SIG(withdraw)
        INVALID_NON_PAYABLE()
        WITHDRAW()
        /// @dev Selectors 0x2f000000 - 0x2fffffff will exceptionally revert.
        /* padding (41) */ stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop
    dest_0x30: __NO_MATCH()
    dest_0x31:
        // 0x313ce567
        __FUNC_SIG(decimals)
        __NON_PAYABLE_SELECTOR_CHECK()
        DECIMALS()
        /* padding (44) */ stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop
    dest_0x32: __NO_MATCH()
    dest_0x33: __NO_MATCH()
    dest_0x34: __NO_MATCH()
    dest_0x35: __NO_MATCH()
    dest_0x36: __NO_MATCH()
    dest_0x37: __NO_MATCH()
    dest_0x38: __NO_MATCH()
    dest_0x39: __NO_MATCH()
    dest_0x3a: __NO_MATCH()
    dest_0x3b: __NO_MATCH()
    dest_0x3c: __NO_MATCH()
    dest_0x3d: __NO_MATCH()
    dest_0x3e: __NO_MATCH()
    dest_0x3f: __NO_MATCH()
    dest_0x40: __NO_MATCH()
    dest_0x41: __NO_MATCH()
    dest_0x42: __NO_MATCH()
    dest_0x43: __NO_MATCH()
    dest_0x44: __NO_MATCH()
    dest_0x45: __NO_MATCH()
    dest_0x46: __NO_MATCH()
    dest_0x47: __NO_MATCH()
    dest_0x48: __NO_MATCH()
    dest_0x49: __NO_MATCH()
    dest_0x4a:
        // 0x4a4089cc
        __FUNC_SIG(withdrawFromTo)
        INVALID_NON_PAYABLE()
        WITHDRAW_FROM_TO()
        /// @dev Selectors 0x4b000000 - 0x4bffffff will exceptionally revert.
        /* padding (03) */ stop stop stop
    dest_0x4c: __NO_MATCH()
    dest_0x4d: __NO_MATCH()
    dest_0x4e: __NO_MATCH()
    dest_0x4f: __NO_MATCH()
    dest_0x50: __NO_MATCH()
    dest_0x51: __NO_MATCH()
    dest_0x52: __NO_MATCH()
    dest_0x53: __NO_MATCH()
    dest_0x54: __NO_MATCH()
    dest_0x55: __NO_MATCH()
    dest_0x56: __NO_MATCH()
    dest_0x57: __NO_MATCH()
    dest_0x58: __NO_MATCH()
    dest_0x59: __NO_MATCH()
    dest_0x5a: __NO_MATCH()
    dest_0x5b: __NO_MATCH()
    dest_0x5c: __NO_MATCH()
    dest_0x5d: __NO_MATCH()
    dest_0x5e: __NO_MATCH()
    dest_0x5f: __NO_MATCH()
    dest_0x60: __NO_MATCH()
    dest_0x61: __NO_MATCH()
    dest_0x62: __NO_MATCH()
    dest_0x63: __NO_MATCH()
    dest_0x64: __NO_MATCH()
    dest_0x65: __NO_MATCH()
    dest_0x66: __NO_MATCH()
    dest_0x67: __NO_MATCH()
    dest_0x68: __NO_MATCH()
    dest_0x69: __NO_MATCH()
    dest_0x6a: __NO_MATCH()
    dest_0x6b: __NO_MATCH()
    dest_0x6c: __NO_MATCH()
    dest_0x6d: __NO_MATCH()
    dest_0x6e: __NO_MATCH()
    dest_0x6f: __NO_MATCH()
    dest_0x70:
        // 0x70a08231
        __FUNC_SIG(balanceOf)
        __NON_PAYABLE_SELECTOR_CHECK()
        BALANCE_OF()
        /* padding (39) */ stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop
    dest_0x71: __NO_MATCH()
    dest_0x72: __NO_MATCH()
    dest_0x73: __NO_MATCH()
    dest_0x74: __NO_MATCH()
    dest_0x75: __NO_MATCH()
    dest_0x76: __NO_MATCH()
    dest_0x77: __NO_MATCH()
    dest_0x78: __NO_MATCH()
    dest_0x79: __NO_MATCH()
    dest_0x7a: __NO_MATCH()
    dest_0x7b: __NO_MATCH()
    dest_0x7c: __NO_MATCH()
    dest_0x7d: __NO_MATCH()
    dest_0x7e: __NO_MATCH()
    dest_0x7f: __NO_MATCH()
    dest_0x80: __NO_MATCH()
    dest_0x81: __NO_MATCH()
    dest_0x82: __NO_MATCH()
    dest_0x83: __NO_MATCH()
    dest_0x84: __NO_MATCH()
    dest_0x85:
        // 0x853828b6
        __FUNC_SIG(withdrawAll)
        __NON_PAYABLE_SELECTOR_CHECK()
        WITHDRAW_ALL()
        /// @dev Selectors 0x86000000 - 0x86ffffff will exceptionally revert.
        /* padding (50) */ stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop 
    dest_0x87: __NO_MATCH()
    dest_0x88: __NO_MATCH()
    dest_0x89: __NO_MATCH()
    dest_0x8a: __NO_MATCH()
    dest_0x8b: __NO_MATCH()
    dest_0x8c: __NO_MATCH()
    dest_0x8d: __NO_MATCH()
    dest_0x8e: __NO_MATCH()
    dest_0x8f: __NO_MATCH()
    dest_0x90: __NO_MATCH()
    dest_0x91: __NO_MATCH()
    dest_0x92: __NO_MATCH()
    dest_0x93: __NO_MATCH()
    dest_0x94:
        // 0x9470b0bd
        __FUNC_SIG(withdrawFrom)
        VALID_NON_PAYABLE()
        _WITHDRAW_FROM_START()
        /* padding (00) */
    dest_0x95:
        // 0x95d89b41
        __FUNC_SIG(symbol)
        __NON_PAYABLE_SELECTOR_CHECK()
        SYMBOL()
        /* padding (08) */ stop stop stop stop stop stop stop stop
    dest_0x96: __NO_MATCH()
    dest_0x97: __NO_MATCH()
    dest_0x98: __NO_MATCH()
    dest_0x99: __NO_MATCH()
    dest_0x9a: __NO_MATCH()
    dest_0x9b: __NO_MATCH()
    dest_0x9c: __NO_MATCH()
    dest_0x9d: __NO_MATCH()
    dest_0x9e: __NO_MATCH()
    dest_0x9f: __NO_MATCH()
    dest_0xa0: __NO_MATCH()
    dest_0xa1: __NO_MATCH()
    dest_0xa2: __NO_MATCH()
    dest_0xa3: __NO_MATCH()
    dest_0xa4: __NO_MATCH()
    dest_0xa5: __NO_MATCH()
    dest_0xa6: __NO_MATCH()
    dest_0xa7: __NO_MATCH()
    dest_0xa8: __NO_MATCH()
    dest_0xa9: 
        // 0xa9059cbb
        __FUNC_SIG(transfer)
        INVALID_NON_PAYABLE()
        TRANSFER()
        /// @dev Selectors 0xaa000000 - 0xaaffffff will exceptionally revert.
        /* padding (49) */ stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop
    dest_0xab: __NO_MATCH()
    dest_0xac: __NO_MATCH()
    dest_0xad:
        // 0xadfd411c
        __FUNC_SIG(sweepLost)
        __NON_PAYABLE_SELECTOR_CHECK()
        SWEEP_LOST()
        /// @dev Selectors 0xae000000 - 0xaeffffff will exceptionally revert.
        /* padding (27) */ stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop
    dest_0xaf: __NO_MATCH()
    dest_0xb0: __NO_MATCH()
    dest_0xb1: __NO_MATCH()
    dest_0xb2: __NO_MATCH()
    dest_0xb3: __NO_MATCH()
    dest_0xb4: __NO_MATCH()
    dest_0xb5: __NO_MATCH()
    dest_0xb6: __NO_MATCH()
    dest_0xb7:
        // 0xb760faf9
        __FUNC_SIG(depositTo)
        __PAYABLE_SELECTOR_CHECK()
        DEPOSIT_TO()
        /* padding (01) */ stop
    dest_0xb8: __NO_MATCH()
    dest_0xb9: __NO_MATCH()
    dest_0xba: __NO_MATCH()
    dest_0xbb: __NO_MATCH()
    dest_0xbc: __NO_MATCH()
    dest_0xbd: __NO_MATCH()
    dest_0xbe: __NO_MATCH()
    dest_0xbf: __NO_MATCH()
    dest_0xc0: __NO_MATCH()
    dest_0xc1: __NO_MATCH()
    dest_0xc2: __NO_MATCH()
    dest_0xc3: __NO_MATCH()
    dest_0xc4: __NO_MATCH()
    dest_0xc5: __NO_MATCH()
    dest_0xc6: __NO_MATCH()
    dest_0xc7: __NO_MATCH()
    dest_0xc8: __NO_MATCH()
    dest_0xc9: __NO_MATCH()
    dest_0xca:
        // 0xca9add8f
        __FUNC_SIG(withdrawAllTo)
        __NON_PAYABLE_SELECTOR_CHECK()
        WITHDRAW_ALL_TO()
        /// @dev Selectors 0xcb000000 - 0xcbffffff will exceptionally revert.
        /* padding (48) */ stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop
    dest_0xcc: __NO_MATCH()
    dest_0xcd: __NO_MATCH()
    dest_0xce: __NO_MATCH()
    dest_0xcf: __NO_MATCH()
    dest_0xd0: 
        // 0xd0e30db0
        __FUNC_SIG(deposit)
        __PAYABLE_SELECTOR_CHECK()
        DEPOSIT()
        /* padding (06) */ stop stop stop stop stop stop
    dest_0xd1: __NO_MATCH()
    dest_0xd2: __NO_MATCH()
    dest_0xd3: __NO_MATCH()
    dest_0xd4: __NO_MATCH()
    dest_0xd5: __NO_MATCH()
    dest_0xd6: __NO_MATCH()
    dest_0xd7: __NO_MATCH()
    dest_0xd8: __NO_MATCH()
    dest_0xd9: __NO_MATCH()
    dest_0xda: __NO_MATCH()
    dest_0xdb: __NO_MATCH()
    dest_0xdc: __NO_MATCH()
    dest_0xdd:
        // 0xdd62ed3e
        __FUNC_SIG(allowance)
        __NON_PAYABLE_SELECTOR_CHECK()
        ALLOWANCE()
        /* padding (34) */ stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop stop stop stop stop stop stop stop stop
                           stop stop stop stop stop stop
    dest_0xde: __NO_MATCH()
    dest_0xdf: __NO_MATCH()
    dest_0xe0: __NO_MATCH()
    dest_0xe1: __NO_MATCH()
    dest_0xe2: __NO_MATCH()
    dest_0xe3: __NO_MATCH()
    dest_0xe4: __NO_MATCH()
    dest_0xe5: __NO_MATCH()
    dest_0xe6: __NO_MATCH()
    dest_0xe7: __NO_MATCH()
    dest_0xe8: __NO_MATCH()
    dest_0xe9: __NO_MATCH()
    dest_0xea: __NO_MATCH()
    dest_0xeb: __NO_MATCH()
    dest_0xec: __NO_MATCH()
    dest_0xed: __NO_MATCH()
    dest_0xee: __NO_MATCH()
    dest_0xef: __NO_MATCH()
    dest_0xf0: __NO_MATCH()
    dest_0xf1: __NO_MATCH()
    dest_0xf2: __NO_MATCH()
    dest_0xf3: __NO_MATCH()
    dest_0xf4: __NO_MATCH()
    dest_0xf5: __NO_MATCH()
    dest_0xf6: __NO_MATCH()
    dest_0xf7: __NO_MATCH()
    dest_0xf8: __NO_MATCH()
    dest_0xf9: __NO_MATCH()
    dest_0xfa: __NO_MATCH()
    dest_0xfb: __NO_MATCH()
    dest_0xfc: __NO_MATCH()
    dest_0xfd: __NO_MATCH()
    dest_0xfe: __NO_MATCH()
    empty_revert: push0 push0 revert

    _TRANSFER_FROM_INF_END()
    _WITHDRAW_FROM_END()
    _WITHDRAW_FROM_TO_INF_END()
}
